buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "com.terrafolio:gradle-jenkins-plugin:1.3.1"
	}
}

apply plugin: "com.terrafolio.jenkins"

Properties properties = new Properties()

def gradleExtPropertiesFile = file("gradle-ext.properties")

properties.load(gradleExtPropertiesFile.newInputStream())

properties.each { property ->
	project.ext.set(property.key, property.value)
}

jenkins {
	servers {
		"is-security-jenkins" {
			url "http://cloud-10-50-0-251"
			username jenkinsUsername
			password jenkinsPassword
		}
	}

	jobs {
		def jobsDirPath = "jobs"

		def configXmlFiles = fileTree(dir: jobsDirPath, include: "**/config.xml").files

		for (def configXmlFile : configXmlFiles) {
			def configXmlDir = configXmlFile.parentFile

			def configXmlDirName = configXmlDir.name

			"$configXmlDirName" {
				server servers."is-security-jenkins"

				definition {
					xml configXmlFile
				}
			}
		}
	}
}