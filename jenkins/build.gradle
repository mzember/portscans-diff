buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "com.terrafolio:gradle-jenkins-plugin:1.3.1"
	}
}

apply plugin: "com.terrafolio.jenkins"

Properties properties = new Properties()

def gradleExtPropertiesFile = file("gradle-ext.properties")

properties.load(gradleExtPropertiesFile.newInputStream())

properties.each { property ->
	project.ext.set(property.key, property.value)
}

jenkins {
	def serverGroups = serverGroupsString.split(",")

	servers {
		for (serverGroup in serverGroups) {
			def serverHost = project."$serverGroup"

			"$serverGroup" {
				url "http://$serverHost"
				username jenkinsUsername
				password jenkinsPassword
			}
		}
	}

	jobs {
		for(def serverGroup : serverGroups) {
			def jobsDirPath = ""

			if (jobsPathOverride) {
				jobsDirPath = jobsPathOverride
			}
			else {
				jobsDirPath = "jobs"
			}

			def configXmlFiles = fileTree(dir: jobsDirPath, include: "**/config.xml").files

			for (def configXmlFile : configXmlFiles) {
				def configXmlDir = configXmlFile.parentFile

				def configXmlDirName = configXmlDir.name

				"$configXmlDirName" {
					server servers."$serverGroup"
					definition {
						xml configXmlFile
					}
				}
			}
		}
	}
}